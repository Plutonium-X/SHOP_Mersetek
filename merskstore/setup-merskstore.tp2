BACKUP ~MerskStore/backup~
AUTHOR ~http://www.baldursgateworld.fr/lacouronne/les-pepites-de-cespenar/12721-pnj-mersetek-le-joaillier.html~

VERSION ~V1.3.2~

README ~merskstore/readme-mersetek_%LANGUAGE%.txt~ ~merskstore/readme-mersetek_english.txt~

// BG2EE TRA compatibility
ALWAYS
ACTION_IF GAME_IS ~bg2ee EET~ THEN BEGIN
  /* It's safe to convert for any language and for all operating systems, no test required */
  // Borrowed from Edwin Romance: convert strings to UTF-8 for BGEE/BG2EE

  // setup.tra contains in game texts as well as installation texts
  // and need to be converted
  ACTION_DEFINE_ARRAY mersetek_noconvert BEGIN END
  ACTION_DEFINE_ARRAY mersetek_reload BEGIN setup END

  LAF HANDLE_CHARSETS
    INT_VAR
      infer_charset = 1
    STR_VAR
      tra_path = EVAL ~%MOD_FOLDER%/tra~
      // charset_table = mersetek_charsets // Use HANDLE_CHARSETS built-in rule for charset choice
      noconvert_array = mersetek_noconvert
      reload_array = mersetek_reload
  END
END
END

AUTO_TRA ~merskstore/tra/%s~

LANGUAGE ~Francais~
         ~francais~ 
         ~merskstore/tra/francais/setup.tra~

LANGUAGE ~Castellano (traducido por Clan REO)~
         ~castellano~ 
         ~merskstore/tra/castellano/setup.tra~

LANGUAGE ~English (by Lollorian)~
         ~english~ 
         ~merskstore/tra/english/setup.tra~

LANGUAGE ~Italian (by Ilot)~
         ~italian~ 
         ~merskstore/tra/italian/setup.tra~

LANGUAGE ~Russian (by Fess)~
         ~russian~ 
         ~merskstore/tra/russian/setup.tra~

BEGIN @0
REQUIRE_PREDICATE GAME_IS ~tob bgt bg2ee EET~ @2

COPY ~merskstore/core/mersetek.cre~         ~override~
  SAY NAME1 @1
  SAY NAME2 @1

COMPILE ~merskstore/core/mersetek.d~

EXTEND_BOTTOM ~ar4500.bcs~  ~merskstore/core/merskarea.baf~

// Isaya
// Il est nécessaire de créer un sac à gemme spécifique pour le magasin de Mersetek
// Faute de préfixe propre au mod, j'ajouter le suffixe MER
COPY_EXISTING ~bag02.itm~ ~override/bag02mer.itm~
COPY_EXISTING ~bag02.sto~ ~override/bag02mer.sto~

COPY ~merskstore/core/mersetek.sto~  ~override~
  SAY NAME2 @1
  // Patch pour remplacer BAG02 (sac déjà vendu par Ribald) par un nouveau
  WRITE_ASCII 0x198 ~bag02mer~ #8
